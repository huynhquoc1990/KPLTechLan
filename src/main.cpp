#include <Arduino.h>
#include <WiFi.h>
#include <PubSubClient.h>
#include <LittleFS.h>
#include <ArduinoJson.h>
#include <ESPAsyncWebServer.h>
#include <esp_task_wdt.h>
#include <esp_system.h>
#include <freertos/FreeRTOS.h>
#include <freertos/task.h>
#include <freertos/queue.h>
#include <freertos/semphr.h>
#include <HTTPClient.h>
#include <WiFiClientSecure.h>
#include <Update.h>

// ============================================================================
// DEBUG LOGGING MACROS
// ============================================================================
#ifdef DEBUG_MODE
  #define DEBUG_PRINT(x)       Serial.print(x)
  #define DEBUG_PRINTLN(x)     Serial.println(x)
  #define DEBUG_PRINTF(...)    Serial.printf(__VA_ARGS__)
#else
  #define DEBUG_PRINT(x)       // No-op in release mode
  #define DEBUG_PRINTLN(x)     // No-op in release mode
  #define DEBUG_PRINTF(...)    // No-op in release mode
#endif

// Critical logs (always enabled, even in release mode)
#define LOG_ERROR(x)           Serial.println(x)
#define LOG_ERROR_F(...)       Serial.printf(__VA_ARGS__)

// Forward declarations for OTA functions
void performOTAUpdate(const char* firmwareURL);
void performOTAUpdateViaAPI(const String& apiEndpoint, const String& ftpUrl);

// Local includes
#include "Settings.h"
#include "structdata.h"
#include "FlashFile.h"
#include "Inits.h"
#include "Api.h"
#include "TTL.h"
#include "Setup.h"
#include "Webservice.h"
#include "WiFiManager.h"
#include "MQTTManager.h"
#include "RS485Manager.h"
#include "SystemManager.h"
#include "FlashFile.h"

// ============================================================================
// GLOBAL VARIABLES - T·ªëi ∆∞u h√≥a memory allocation
// ============================================================================

// Core system objects - s·ª≠ d·ª•ng smart pointers n·∫øu c√≥ th·ªÉ
static DeviceStatus deviceStatus;
static CompanyInfo companyInfo;
static Settings settings;
static TimeSetup timeSetup;
static GetIdLogLoss receivedMessage;
bool inConfigPortal = false;

// Nozzle prices storage
static NozzlePrices nozzlePrices;

// System state
static uint32_t currentId = 0;
static bool statusConnected = false;
static bool isLoggedIn = false;
static bool mqttTopicsConfigured = false;
static bool mqttSubscribed = false; // Track subscription state
static unsigned long counterReset = 0;
static unsigned long lastHeapCheck = 0;
static String systemStatus = "OK";
static String lastError = "";

// MQTT topics - t·ªëi ∆∞u memory allocation
static char fullTopic[64];
static char topicStatus[64];
static char topicError[64];           // full topic with device id (for publish if needed)
static char topicErrorSub[64];        // wildcard subscription (e.g., 11223311A/Error/#)
static char topicErrorPrefix[64];     // prefix match (e.g., 11223311A/Error/)
static char topicRestart[64];
static char topicGetLogIdLoss[64];
static char topicShift[64];
static char topicChange[64];
static char topicOTA[64];             // topic for OTA firmware update
static char topicUpdatePrice[64];     // topic for changing price
static char topicGetPrice[64];        // topic for requesting current prices
static char topicRequestLog[64];      // topic for requesting logs from Flash

// FreeRTOS objects
static QueueHandle_t mqttQueue = NULL;
static QueueHandle_t logIdLossQueue = NULL;
static QueueHandle_t priceChangeQueue = NULL;  // Queue for price change requests
static SemaphoreHandle_t flashMutex = NULL;
static SemaphoreHandle_t systemMutex = NULL;

// Track last sent price change request for response publishing
static PriceChangeRequest lastPriceChangeRequest;

// Task handles
static TaskHandle_t rs485TaskHandle = NULL;
static TaskHandle_t mqttTaskHandle = NULL;
static TaskHandle_t wifiTaskHandle = NULL;
static TaskHandle_t webServerTaskHandle = NULL;
static TaskHandle_t resendLogRequestTaskHandle = NULL;

// WiFi objects
static WiFiClient wifiClient;
static PubSubClient mqttClient(wifiClient);
static AsyncWebServer webServer(80);
WiFiManager * wifiManager = nullptr;

// Reset button state
static struct
{
  unsigned long pressTime = 0;
  bool isPressed = false;
  bool inProgress = false;
} resetButton;

// ============================================================================
// FORWARD DECLARATIONS
// ============================================================================

// Task functions
void rs485Task(void *parameter);
void mqttTask(void *parameter);
void wifiTask(void *parameter);
void webServerTask(void *parameter);
void wifiRescanTask(void *parameter);

// System functions
void systemInit();
void systemCheck();
void handleResetButton();
void checkHeap();
void setupTime();
void setSystemStatus(const String &status, const String &error = "");
void adjustThermals();

// MQTT functions
void mqttCallback(char *topic, byte *payload, unsigned int length);
void setupMQTTTopics();
void connectMQTT();
void sendDeviceStatus();

// Missing function declarations
void sendLogRequest(uint32_t logId);
void processAllVoi(TimeSetup *time);
uint8_t calculateChecksum_LogData(const uint8_t *data, size_t length);
void ganLog(byte *buffer, PumpLog &log);
void ConnectedKPLBox(void *param);
void resendLogRequest(void *param);
void blinkOutput2Connected();
void BlinkOutput2Task(void *param);
void readMacEsp();
// void performOTAUpdate(const char* firmwareURL);
void performOTAUpdateViaAPI(const String& apiEndpoint, const String& ftpUrl);
void sendPriceChangeCommand(const PriceChangeRequest &request);

// ============================================================================
// MAIN SETUP & LOOP
// ============================================================================

// d√πng ƒë·ªÉ ki·ªÉm tra c√≥ ph√°t sinh giao d·ªãch ko, n·∫øu c√≥ reset bi·∫øn n√†y. v√† 60 ph√∫t n·∫øu kh√¥ng c√≥ giao d·ªãch th√¨ restart esp
unsigned long checkLogSend = 0;


void setup()
{
  Serial.begin(115200);
  delay(1000);
  Serial.println("\n\n===========================================");
  Serial.println("=== KPL Gas Device Starting ===");
  
  // Check reset reason
  esp_reset_reason_t reset_reason = esp_reset_reason();
  Serial.printf("Reset reason: %d ", reset_reason);
  switch(reset_reason)
  {
    case ESP_RST_UNKNOWN:   Serial.println("(UNKNOWN)"); break;
    case ESP_RST_POWERON:   Serial.println("(POWER ON)"); break;
    case ESP_RST_EXT:       Serial.println("(EXTERNAL PIN)"); break;
    case ESP_RST_SW:        Serial.println("(SOFTWARE)"); break;
    case ESP_RST_PANIC:     Serial.println("(PANIC/EXCEPTION)"); break;
    case ESP_RST_INT_WDT:   Serial.println("(‚ö†Ô∏è INTERRUPT WDT)"); break;
    case ESP_RST_TASK_WDT:  Serial.println("(‚ö†Ô∏è TASK WDT TIMEOUT)"); break;
    case ESP_RST_WDT:       Serial.println("(‚ö†Ô∏è OTHER WDT)"); break;
    case ESP_RST_DEEPSLEEP: Serial.println("(DEEP SLEEP)"); break;
    case ESP_RST_BROWNOUT:  Serial.println("(BROWNOUT)"); break;
    case ESP_RST_SDIO:      Serial.println("(SDIO)"); break;
    default:                Serial.println("(OTHER)"); break;
  }
  Serial.println("===========================================\n");
  
  // Initialize system
  systemInit();
  // Create tasks with optimized stack sizes
  xTaskCreatePinnedToCore(rs485Task, "RS485", 8192, NULL, 3, &rs485TaskHandle, 0);
  xTaskCreatePinnedToCore(wifiTask, "WiFi", 8192, NULL, 2, &wifiTaskHandle, 1);
  xTaskCreatePinnedToCore(mqttTask, "MQTT", 8192, NULL, 2, &mqttTaskHandle, 1);
  // xTaskCreatePinnedToCore(resendLogRequest, "ResendLogRequest", 8192, NULL, 2, &resendLogRequestTaskHandle, 1);
  
  Serial.println("System initialized successfully");
}

void loop()
{
  // Main loop ch·ªâ x·ª≠ l√Ω c√°c t√°c v·ª• nh·∫π
  handleResetButton();
  systemCheck();

  // Yield cho c√°c task kh√°c
  vTaskDelay(pdMS_TO_TICKS(100));
  yield();
  // Feed watchdog for loopTask explicitly
  esp_task_wdt_reset();
}

void readMacEsp()
{
  Serial.println("Reading Mac ESP...");
  // read ƒë·ªãa ch·ªâ mac c·ªßa esp
  uint64_t chipid = ESP.getEfuseMac();
  char macStr[18];
  sprintf(macStr, "%02X:%02X:%02X:%02X:%02X:%02X",
          (uint8_t)(chipid >> 40), (uint8_t)(chipid >> 32), (uint8_t)(chipid >> 24),
          (uint8_t)(chipid >> 16), (uint8_t)(chipid >> 8), (uint8_t)chipid);
  Serial.println("Mac: " + String(macStr));

  // call api ƒë·ªÉ check mac c√≥ trong h·ªá th·ªëng hay kh√¥ng, s·ª≠ d·ª•ng Ph∆∞∆°ng th·ª©c Poss

  String url = String(API_BASE_URL) + API_CHECK_MAC_ENDPOINT;
  HTTPClient http;
    http.begin(url);
  http.addHeader("Content-Type", "application/json");
  DynamicJsonDocument doc(1024);
  doc["taikhoan"] = String(macStr);
  doc["idchinhanh"] = String(companyInfo.Mst);
  String jsonData;
  serializeJson(doc, jsonData); 
  Serial.println("jsonData get Mac: " + jsonData);
  int httpCode = http.POST(jsonData);

  // c·∫ßn ki·ªÉm tra n·ªôi dung tr·∫£ v·ªÅ n·∫øu 'OK' th√¨ ƒë·ªçc gi√° tr·ªã l√™n
  String response = http.getString();
  Serial.println("response: " + response);
  // check response c√≥ ch·ª©a 'OK' kh√¥ng [{"IsValid":true}]
  DynamicJsonDocument doc2(1024);  
  DeserializationError error = deserializeJson(doc2, response);
  Serial.println("error: " + String(error.c_str()));

  if (error && httpCode != 200) {
    Serial.println("Parse error");
    http.end();
    return;
  }
  
  bool isValid = false;
  if (doc2.is<JsonArray>()) {
    JsonArray arr = doc2.as<JsonArray>();
    if (!arr.isNull() && arr.size() > 0 && arr[0].containsKey("IsValid")) {
      isValid = arr[0]["IsValid"].as<bool>();
    }
  } else if (doc2.is<JsonObject>()) {
    JsonObject obj = doc2.as<JsonObject>();
    if (obj.containsKey("IsValid")) {
      isValid = obj["IsValid"].as<bool>();
    }
  }

  http.end();

  if (isValid) {
    Serial.println("Mac is in system");
  } else {
    Serial.println("Mac is not in system");
    while (true)
    {
      Serial.println("Mac is not in system");
      vTaskDelay(pdMS_TO_TICKS(1000));
    }
  }
  // http.end();
}

// ============================================================================
// SYSTEM INITIALIZATION
// ============================================================================

void systemInit()
{
  // GPIO setup
  pinMode(OUT1, OUTPUT);
  pinMode(OUT2, OUTPUT);
  pinMode(RESET_CONFIG_PIN, INPUT_PULLUP);

  // Watchdog setup - 30s timeout with panic on timeout
  esp_task_wdt_init(30, true); // 30s timeout, true = panic and reset on timeout
  esp_task_wdt_add(NULL); // Add setup/loop task to WDT

  // Initialize RS485
  Serial2.begin(RS485BaudRate, SERIAL_8N1, RX_PIN, TX_PIN);

  // Initialize FreeRTOS objects
  mqttQueue = xQueueCreate(10, sizeof(PumpLog));
  logIdLossQueue = xQueueCreate(50, sizeof(DtaLogLoss));
  priceChangeQueue = xQueueCreate(10, sizeof(PriceChangeRequest));  // Queue for price changes
  flashMutex = xSemaphoreCreateMutex();
  systemMutex = xSemaphoreCreateMutex();

  if (!mqttQueue || !logIdLossQueue || !priceChangeQueue || !flashMutex || !systemMutex)
  {
    Serial.println("ERROR: Failed to create FreeRTOS objects!");
    setSystemStatus("ERROR", "Failed to create FreeRTOS objects");
    ESP.restart();
  }

  // Initialize file system
  if (!initLittleFS())
  {
    Serial.println("ERROR: Failed to initialize LittleFS!");
    setSystemStatus("ERROR", "Failed to initialize LittleFS");
    ESP.restart();
  }

  // Initialize WiFiManager
  wifiManager = new WiFiManager(&webServer);

  // Load system data
  readFlashSettings(flashMutex, deviceStatus, counterReset);
  currentId = initializeCurrentId(flashMutex);
  
  // Load nozzle prices from Flash
  Serial.println("Loading nozzle prices from Flash...");
  if (loadNozzlePrices(nozzlePrices, flashMutex)) {
    printNozzlePrices(nozzlePrices);
  } else {
    Serial.println("No saved prices found, initializing with defaults (0.0)");
    // Save initial default prices
    saveNozzlePrices(nozzlePrices, flashMutex);
  }

  // Initialize WiFi
  WiFi.mode(WIFI_STA);

  // Initialize MQTT - will be updated from API settings
  mqttClient.setServer(mqttServer, 1883); // Default server, will be updated from API
  mqttClient.setBufferSize(1024); // Increase buffer size from default 128
  mqttClient.setCallback(mqttCallback);
  
  Serial.printf("MQTT client initialized - Buffer size: %d\n", mqttClient.getBufferSize());

  // Send startup commands
  sendStartupCommand();
  delay(1000);
  sendStartupCommand();

  Serial.printf("System initialized - Current ID: %u\n", currentId);
}

// ============================================================================
// SYSTEM MONITORING
// ============================================================================

void systemCheck()
{
  static unsigned long lastCheck = 0;
  unsigned long now = millis();

  // Check every 10 seconds
  if (now - lastCheck >= 10000)
  {
    lastCheck = now;

    // Reset watchdog
    esp_task_wdt_reset();

    // Check heap memory
    checkHeap();

    checkLogSend++;
    if (checkLogSend >= 360)
    {
      ESP.restart();
      Serial.println("Check log send: " + String(checkLogSend));
    }

    // Adjust thermals based on die temperature
    adjustThermals();

    // Send status via MQTT if connected and topics configured
    if (mqttClient.connected() && mqttTopicsConfigured)
    {
      sendDeviceStatus();
    }

    // Check task states
    if (rs485TaskHandle && eTaskGetState(rs485TaskHandle) == eDeleted)
    {
      Serial.println("WARNING: RS485 task deleted, restarting...");
      setSystemStatus("WARNING", "RS485 task deleted and restarted");
      xTaskCreatePinnedToCore(rs485Task, "RS485", 8192, NULL, 3, &rs485TaskHandle, 0);
    }
  }
}

void checkHeap()
{
  size_t freeHeap = heap_caps_get_free_size(MALLOC_CAP_8BIT);
  size_t minFreeHeap = heap_caps_get_minimum_free_size(MALLOC_CAP_8BIT);

  deviceStatus.heap = freeHeap;
  deviceStatus.free = minFreeHeap;
  deviceStatus.temperature = temperatureRead();
  // Optional: smooth die temperature to avoid spikes
  static float smoothedTemp = 0.0f;
  if (smoothedTemp == 0.0f) smoothedTemp = deviceStatus.temperature;
  smoothedTemp = 0.7f * smoothedTemp + 0.3f * deviceStatus.temperature;
  deviceStatus.temperature = smoothedTemp;
  deviceStatus.counterReset = counterReset;

  // Log memory status (debug only)
  DEBUG_PRINTF("Heap: %u free, %u min free, Temp: %.1f¬∞C\n",
                freeHeap, minFreeHeap, deviceStatus.temperature);

  // Memory warning (always log critical errors)
  if (freeHeap < 10000)
  {
    LOG_ERROR("WARNING: Low memory!");
    setSystemStatus("WARNING", "Low memory: " + String(freeHeap) + " bytes");
  }
  else if (systemStatus == "WARNING" && lastError.indexOf("Low memory") >= 0)
  {
    // Clear memory warning if heap is back to normal
    setSystemStatus("OK");
  }
}

void adjustThermals()
{
  static bool thermallyReduced = false;
  float dieTemp = deviceStatus.temperature;

  // In configuration portal, force minimal performance to keep temps low
  if (inConfigPortal)
  {
    setCpuFrequencyMhz(80);
    // Keep AP stable: disable modem sleep in AP mode
    WiFi.setSleep(false);
    WiFi.setTxPower(WIFI_POWER_8_5dBm);
    if (!thermallyReduced)
    {
      Serial.println("Thermal: CONFIG portal - CPU 80MHz, TX power 8.5dBm, modem sleep OFF");
      thermallyReduced = true;
    }
    return;
  }

  if (!thermallyReduced && dieTemp > 80.0f)
  {
    setCpuFrequencyMhz(160);
    WiFi.setSleep(true);
    WiFi.setTxPower(WIFI_POWER_2dBm);
    thermallyReduced = true;
    Serial.println("Thermal: reduced CPU to 160MHz, TX power 2dBm, modem sleep ON");
  }
  else if (thermallyReduced && dieTemp < 70.0f)
  {
    inConfigPortal = false;
    setCpuFrequencyMhz(240);
    WiFi.setTxPower(WIFI_POWER_8_5dBm);
    thermallyReduced = false;
    Serial.println("Thermal: restored CPU to 240MHz, TX power 8.5dBm");
  }
}

void setSystemStatus(const String &status, const String &error)
{
  systemStatus = status;
  lastError = error;

  if (status != "OK")
  {
    Serial.printf("[STATUS] %s: %s\n", status.c_str(), error.c_str());
    // send status to mqtt
    if (mqttClient.connected())
    {
      sendDeviceStatus();
    }
    // mqttClient.loop();
  }
}

// ============================================================================
// RESET BUTTON HANDLING
// ============================================================================

void handleResetButton()
{
  int buttonState = digitalRead(RESET_CONFIG_PIN);

  if (buttonState == LOW)
  { // Button pressed
    if (!resetButton.isPressed)
    {
      resetButton.isPressed = true;
      resetButton.pressTime = millis();
      Serial.println("Reset button pressed - hold for 5 seconds");
      digitalWrite(OUT2, HIGH);
    }

    // Show countdown
    unsigned long pressDuration = millis() - resetButton.pressTime;
    if (pressDuration >= 5000 && !resetButton.inProgress)
    {
      resetButton.inProgress = true;
      Serial.println("Resetting WiFi configuration...");

      // Visual feedback
      for (int i = 0; i < 5; i++)
      {
        digitalWrite(OUT2, HIGH);
        delay(200);
        digitalWrite(OUT2, LOW);
        delay(200);
      }

      // Reset config
      if (LittleFS.exists("/config.txt"))
      {
        LittleFS.remove("/config.txt");
        Serial.println("WiFi config deleted");
      }

      Serial.println("Restarting in 3 seconds...");
      delay(3000);
      ESP.restart();
    }
  }
  else
  {
    if (resetButton.isPressed)
    {
      unsigned long pressDuration = millis() - resetButton.pressTime;
      resetButton.isPressed = false;
      resetButton.inProgress = false;
      digitalWrite(OUT2, LOW);
      Serial.println("Reset button released");

      // Short press: trigger WiFi rescan if in config portal (AP mode)
      if (pressDuration < 1000 && inConfigPortal && wifiManager != nullptr)
      {
        Serial.println("Short press detected - triggering WiFi rescan...");
        xTaskCreate(wifiRescanTask, "WiFiRescan", 4096, NULL, 2, NULL);
      }
    }
  }
}

// ============================================================================
// WIFI TASK
// ============================================================================

void wifiTask(void *parameter)
{
  Serial.println("WiFi task started");
  esp_task_wdt_add(NULL);

  // Ch·ªâ ki·ªÉm tra config m·ªôt l·∫ßn khi kh·ªüi ƒë·ªông
  if (!wifiManager->checkWiFiConfig())
  {
    Serial.println("No valid WiFi config found. Entering AP configuration mode.");

    // T·∫°m d·ª´ng c√°c task kh√°c ƒë·ªÉ ∆∞u ti√™n
    if (rs485TaskHandle != NULL)
      vTaskSuspend(rs485TaskHandle);
    if (mqttTaskHandle != NULL)
      vTaskSuspend(mqttTaskHandle);

    // B·∫≠t ch·∫ø ƒë·ªô AP, qu√©t Wi-Fi m·ªôt l·∫ßn v√† kh·ªüi ƒë·ªông web server
    wifiManager->startConfigurationPortal();

    Serial.println("AP Mode and Web Server started. WiFi task will now suspend.");

    // Restore CPU when leaving config portal (will resume after restart)
    setCpuFrequencyMhz(240);
    // Deregister from WDT before suspending indefinitely
    esp_task_wdt_delete(NULL);
    // T·ª± treo task n√†y, ch·ªù restart t·ª´ web server
    vTaskSuspend(NULL);
  }

  // N·∫øu c√≥ config, ti·∫øp t·ª•c v√≤ng l·∫∑p k·∫øt n·ªëi b√¨nh th∆∞·ªùng
  while (true)
  {
    esp_task_wdt_reset();
    if (WiFi.status() != WL_CONNECTED)
    {
      Serial.println("Attempting to connect to saved WiFi...");
      if (wifiManager->connectToWiFi())
      {
        Serial.println("WiFi connected successfully.");
        setupTime();
        
        // Only setup MQTT topics if not already configured
        if (!mqttTopicsConfigured)
        {
          Serial.println("Setting up MQTT topics for first time...");
          setupMQTTTopics();
        }
        else
        {
          Serial.println("MQTT topics already configured, skipping setup");
        }
        
        readMacEsp();
        statusConnected = true;
      }
      else
      {
        static uint32_t cooldownSeconds = 10; // exponential backoff, capped
        Serial.printf("WiFi connection failed, cooling radio for %lus...\n", cooldownSeconds);
        setSystemStatus("ERROR", "WiFi connection failed");
        
        // Reset MQTT state when WiFi fails
        mqttTopicsConfigured = false;
        statusConnected = false;
        
        // Unsubscribe from MQTT topics before disconnecting
        if (mqttClient.connected() && mqttSubscribed)
        {
          Serial.println("WiFi lost - unsubscribing from MQTT topics...");
          mqttClient.unsubscribe(topicErrorSub);
          mqttClient.unsubscribe(topicRestart);
          mqttClient.unsubscribe(topicGetLogIdLoss);
          mqttClient.unsubscribe(topicChange);
          mqttClient.unsubscribe(topicShift);
          mqttClient.unsubscribe(topicOTA);
          mqttClient.unsubscribe(topicUpdatePrice);
          mqttClient.disconnect();
          mqttSubscribed = false;
          Serial.println("MQTT topics unsubscribed and disconnected");
        }

        // Turn radio off during cooldown to reduce temperature
        WiFi.disconnect(true, true);
        WiFi.mode(WIFI_OFF);
        setCpuFrequencyMhz(160);
        for (uint32_t i = 0; i < cooldownSeconds; i++) {
          esp_task_wdt_reset();
          vTaskDelay(pdMS_TO_TICKS(1000));
        }

        // Restore for next attempt
        WiFi.mode(WIFI_STA);
        WiFi.setSleep(true);
        setCpuFrequencyMhz(240);

        // Increase backoff up to 60s
        if (cooldownSeconds < 60) {
          cooldownSeconds = cooldownSeconds < 30 ? cooldownSeconds * 2 : 60;
        }
      }
    }
    else
    {
      // ƒê√£ k·∫øt n·ªëi, ki·ªÉm tra ƒë·ªãnh k·ª≥ trong 30 step, m·ªói step reset WDT
      for (int i = 0; i < 30; i++) {
        esp_task_wdt_reset();
        vTaskDelay(pdMS_TO_TICKS(1000));
      }
      
      // Log WiFi/MQTT status periodically
      static unsigned long lastStatusLog = 0;
      if (millis() - lastStatusLog > 60000) // Every 60 seconds
      {
        Serial.printf("WiFi Status: %s, MQTT Topics: %s, MQTT Connected: %s\n", 
                     WiFi.status() == WL_CONNECTED ? "CONNECTED" : "DISCONNECTED",
                     mqttTopicsConfigured ? "CONFIGURED" : "NOT_CONFIGURED",
                     mqttClient.connected() ? "CONNECTED" : "DISCONNECTED");
        lastStatusLog = millis();
      }
    }
    yield();
  }
}

// ============================================================================
// MQTT TASK
// ============================================================================

void mqttTask(void *parameter)
{
  Serial.println("MQTT task started");
  // esp_task_wdt_add(NULL);

  while (true)
  {
    if (WiFi.status() == WL_CONNECTED)
    {
      // Wait for MQTT topics to be configured before attempting connection
      if (!mqttTopicsConfigured)
      {
        Serial.println("MQTT topics not configured yet, waiting...");
        vTaskDelay(pdMS_TO_TICKS(1000));
        esp_task_wdt_reset();
        continue;
      }
      
      if (!mqttClient.connected())
      {
        connectMQTT();
      }
      else
      {
        // Process MQTT queue
        PumpLog log;
        if (xQueueReceive(mqttQueue, &log, pdMS_TO_TICKS(100)) == pdTRUE)
        {
          sendMQTTData(log);
        }
        
        // Additional loop call to ensure callback processing
        mqttClient.loop();
      }
    }
    else
    {
      // WiFi disconnected - unsubscribe and disconnect MQTT
      if (mqttClient.connected() && mqttSubscribed)
      {
        Serial.println("WiFi disconnected - cleaning up MQTT subscriptions...");
        mqttClient.unsubscribe(topicErrorSub);
        mqttClient.unsubscribe(topicRestart);
        mqttClient.unsubscribe(topicGetLogIdLoss);
        mqttClient.unsubscribe(topicChange);
        mqttClient.unsubscribe(topicShift);
        mqttClient.unsubscribe(topicOTA);
        mqttClient.unsubscribe(topicUpdatePrice);
        mqttClient.unsubscribe(topicGetPrice);
        mqttClient.disconnect();
        mqttSubscribed = false;
        Serial.println("MQTT cleanup completed");
      }
    }

    // System monitoring (every 10 seconds)
    systemCheck();

    vTaskDelay(pdMS_TO_TICKS(100));
    // esp_task_wdt_reset();
    yield();
  }
}

// ============================================================================
// RS485 TASK
// ============================================================================

void rs485Task(void *parameter)
{
  Serial.println("RS485 task started");

  // Wait for system to stabilize before processing RS485 data
  Serial.println("[RS485] Waiting 3 seconds for system stabilization...");
  vTaskDelay(pdMS_TO_TICKS(3000));
  
  // Clear any buffered data from TTL boot sequence
  Serial.println("[RS485] Clearing boot buffer...");
  while (Serial2.available())
  {
    Serial2.read();
  }
  Serial.println("[RS485] Buffer cleared, ready to process data");

  byte buffer[LOG_SIZE];
  unsigned long lastSendTime = 0;
  unsigned long lastPriceChangeTime = 0;
  esp_task_wdt_add(NULL); // Add this task to WDT monitoring
  
  while (true)
  {
    // Feed watchdog at start of each cycle
    esp_task_wdt_reset();
    
    // Read RS485 data with error protection
    readRS485Data(buffer);

    // Priority 1: Process price change queue (one request per cycle, every 300ms)
    if (millis() - lastPriceChangeTime >= 300)
    {
      lastPriceChangeTime = millis();
      PriceChangeRequest priceRequest;
      if (xQueueReceive(priceChangeQueue, &priceRequest, 0) == pdTRUE)
      {
        Serial.printf("[RS485] Processing price change request for DeviceID=%d (Queue size: %d)\n", 
                      priceRequest.deviceId, uxQueueMessagesWaiting(priceChangeQueue));
        sendPriceChangeCommand(priceRequest);
      }
    }

    // Priority 2: Send log requests every 500ms (reduce heat/power)
    if (millis() - lastSendTime >= 500)
    {
      lastSendTime = millis();
      // Process log loss queue
      DtaLogLoss dataLog;
      if (xQueueReceive(logIdLossQueue, &dataLog, 0) == pdTRUE)
      {
        checkLogSend = 0; // d√πng ƒë·ªÉ ki·ªÉm tra c√≥ ph√°t sinh giao d·ªãch ko, n·∫øu c√≥ reset bi·∫øn n√†y. 
        sendLogRequest(static_cast<uint32_t>(dataLog.Logid));
      }
    }

    vTaskDelay(pdMS_TO_TICKS(10));
    yield();
  }
}

// ============================================================================
// HELPER FUNCTIONS
// ============================================================================

void setupTime()
{
  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);

  struct tm timeinfo;
  int retry = 0;
  const int maxRetries = 5;

  while (!getLocalTime(&timeinfo) && retry < maxRetries)
  {
    Serial.println("Failed to obtain time, retrying...");
    delay(2000);
    configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);
    retry++;
  }

  if (retry < maxRetries)
  {
    setUpTime(&timeSetup, timeinfo);
    processAllVoi(&timeSetup);
    Serial.println("Time synchronized successfully");
    setSystemStatus("OK", ""); // Clear any previous time sync errors
  }
  else
  {
    Serial.println("Failed to sync time");
    setSystemStatus("WARNING", "Failed to synchronize time after " + String(maxRetries) + " attempts");
  }
}

void setupMQTTTopics()
{
  // Get MQTT settings from API
  callAPIGetSettingsMqtt(&settings, flashMutex);
  
  // Update MQTT server with settings from API
  if (strlen(settings.MqttServer) > 0)
  {
    // Disconnect if currently connected to force reconnection with new server
    if (mqttClient.connected())
    {
      mqttClient.disconnect();
      Serial.println("MQTT disconnected to update server settings");
    }
    
    mqttClient.setServer(settings.MqttServer, settings.PortMqtt);
    // Serial.printf("MQTT server updated from API: %s:%d\n", settings.MqttServer, settings.PortMqtt);
  }
  else
  {
    Serial.println("Using default MQTT server (no API settings)");
  }
  
  // Get company info
  xTaskCreate(callAPIServerGetCompanyInfo, "getCompanyInfo", 2048, &companyInfo, 2, NULL);

  // Wait for company info
  vTaskDelay(pdMS_TO_TICKS(4000));

  // Build topics
  snprintf(fullTopic, sizeof(fullTopic), "%s%s%s", companyInfo.Mst, TopicSendData, TopicMqtt);
  snprintf(topicStatus, sizeof(topicStatus), "%s%s%s", companyInfo.Mst, TopicStatus, TopicMqtt);
  snprintf(topicError, sizeof(topicError), "%s%s%s", companyInfo.Mst, TopicLogError, TopicMqtt);
  // wildcard subscribe to all device ids under Error channel
  snprintf(topicErrorSub, sizeof(topicErrorSub), "%s%s#", companyInfo.Mst, TopicLogError);
  // prefix for quick check in callback
  snprintf(topicErrorPrefix, sizeof(topicErrorPrefix), "%s%s", companyInfo.Mst, TopicLogError);
  snprintf(topicRestart, sizeof(topicRestart), "%s%s%s", companyInfo.Mst, TopicRestart, TopicMqtt);
  snprintf(topicGetLogIdLoss, sizeof(topicGetLogIdLoss), "%s%s", companyInfo.Mst, TopicGetLogIdLoss);
  snprintf(topicShift, sizeof(topicShift), "%s%s%s", companyInfo.Mst, TopicShift, TopicMqtt);
  snprintf(topicChange, sizeof(topicChange), "%s%s%s", companyInfo.Mst, TopicChange, TopicMqtt);
  snprintf(topicOTA, sizeof(topicOTA), "%s/OTA/%s", companyInfo.Mst, TopicMqtt);
  
  snprintf(topicUpdatePrice, sizeof(topicUpdatePrice), "%s%s", companyInfo.CompanyId, TopicUpdatePrice);
  snprintf(topicGetPrice, sizeof(topicGetPrice), "%s%s", companyInfo.Mst, TopicGetPrice);
  // snprintf(topicUpdatePrice, sizeof(topicUpdatePrice), "%s%s%s", companyInfo.CompanyId, TopicUpdatePrice);

  Serial.printf("MQTT topics configured - Company ID: %s (MST: %s)\n", companyInfo.CompanyId, companyInfo.Mst);
  mqttTopicsConfigured = true;

  // Subscribe to topics if MQTT is connected
  if (mqttClient.connected())
  {
    Serial.println("=== SUBSCRIBING TO MQTT TOPICS ===");
    
    bool sub1 = mqttClient.subscribe(topicErrorSub);
    bool sub2 = mqttClient.subscribe(topicRestart);
    bool sub3 = mqttClient.subscribe(topicGetLogIdLoss);
    bool sub4 = mqttClient.subscribe(topicChange);
    bool sub5 = mqttClient.subscribe(topicShift);
    bool sub6 = mqttClient.subscribe(topicOTA);
    bool sub7 = mqttClient.subscribe(topicUpdatePrice);
    bool sub8 = mqttClient.subscribe(topicGetPrice);

    Serial.printf("Subscription results:\n");
    Serial.printf("  Error (%s): %s\n", topicError, sub1 ? "SUCCESS" : "FAILED");
    Serial.printf("  Restart (%s): %s\n", topicRestart, sub2 ? "SUCCESS" : "FAILED");
    Serial.printf("  GetLogIdLoss (%s): %s\n", topicGetLogIdLoss, sub3 ? "SUCCESS" : "FAILED");
    Serial.printf("  Change (%s): %s\n", topicChange, sub4 ? "SUCCESS" : "FAILED");
    Serial.printf("  Shift (%s): %s\n", topicShift, sub5 ? "SUCCESS" : "FAILED");
    Serial.printf("  OTA (%s): %s\n", topicOTA, sub6 ? "SUCCESS" : "FAILED");
    Serial.printf("  UpdatePrice (%s): %s\n", topicUpdatePrice, sub7 ? "SUCCESS" : "FAILED");
    Serial.printf("  GetPrice (%s): %s\n", topicGetPrice, sub8 ? "SUCCESS" : "FAILED");
    Serial.println("=== SUBSCRIPTION COMPLETE ===");
    
    // Set subscription flag
    mqttSubscribed = (sub1 && sub2 && sub3 && sub4 && sub5 && sub6 && sub7 && sub8);
    Serial.printf("MQTT subscription state: %s\n", mqttSubscribed ? "SUBSCRIBED" : "PARTIAL_FAILURE");
      }
      else
      {
    Serial.println("MQTT not connected, will subscribe when connected");
  }
}

void connectMQTT()
{
  Serial.println("Connecting to MQTT...");
  
  // Set connection timeout
  mqttClient.setKeepAlive(60);
  mqttClient.setSocketTimeout(15);

  if (mqttClient.connect(TopicMqtt, mqttUser, mqttPassword))
  {
    Serial.println("MQTT connected");
    statusConnected = true;
    setSystemStatus("OK", ""); // Clear any MQTT connection errors

    // Subscribe to topics if they have been configured
    if (mqttTopicsConfigured)
    {
      Serial.println("=== RE-SUBSCRIBING TO MQTT TOPICS AFTER RECONNECT ===");
      
      bool sub1 = mqttClient.subscribe(topicErrorSub);
      bool sub2 = mqttClient.subscribe(topicRestart);
      bool sub3 = mqttClient.subscribe(topicGetLogIdLoss);
      bool sub4 = mqttClient.subscribe(topicChange);
      bool sub5 = mqttClient.subscribe(topicShift);
      bool sub6 = mqttClient.subscribe(topicOTA);
      bool sub7 = mqttClient.subscribe(topicUpdatePrice);
      bool sub8 = mqttClient.subscribe(topicGetPrice);

      Serial.printf("Re-subscription results:\n");
      Serial.printf("  Error (%s): %s\n", topicError, sub1 ? "SUCCESS" : "FAILED");
      Serial.printf("  Restart (%s): %s\n", topicRestart, sub2 ? "SUCCESS" : "FAILED");
      Serial.printf("  GetLogIdLoss (%s): %s\n", topicGetLogIdLoss, sub3 ? "SUCCESS" : "FAILED");
      Serial.printf("  Change (%s): %s\n", topicChange, sub4 ? "SUCCESS" : "FAILED");
      Serial.printf("  Shift (%s): %s\n", topicShift, sub5 ? "SUCCESS" : "FAILED");
      Serial.printf("  OTA (%s): %s\n", topicOTA, sub6 ? "SUCCESS" : "FAILED");
      Serial.printf("  UpdatePrice (%s): %s\n", topicUpdatePrice, sub7 ? "SUCCESS" : "FAILED");
      Serial.printf("  GetPrice (%s): %s\n", topicGetPrice, sub8 ? "SUCCESS" : "FAILED");
      Serial.println("=== RE-SUBSCRIPTION COMPLETE ===");
      
      // Set subscription flag
      mqttSubscribed = (sub1 && sub2 && sub3 && sub4 && sub5 && sub6 && sub7 && sub8);
      Serial.printf("MQTT re-subscription state: %s\n", mqttSubscribed ? "SUBSCRIBED" : "PARTIAL_FAILURE");
      
      // Publish saved prices from Flash after successful MQTT connection
      Serial.println("\n=== PUBLISHING SAVED PRICES FROM FLASH ===");
      publishSavedPricesToMQTT(nozzlePrices, mqttClient, companyInfo.CompanyId, 
                              companyInfo.Mst, TopicMqtt);
      Serial.println("=== SAVED PRICES PUBLISHED ===\n");
    }
  }
  else
  {
    Serial.printf("MQTT connection failed. State: %d\n", mqttClient.state());
    setSystemStatus("ERROR", "MQTT connection failed - state: " + String(mqttClient.state()));
    vTaskDelay(pdMS_TO_TICKS(5000));
  }
}

void sendDeviceStatus()
{
  // Create JSON status data
  DynamicJsonDocument doc(1024);

  doc["idDevice"] = TopicMqtt;
  doc["companyId"] = companyInfo.Mst;
  doc["heap"] = deviceStatus.heap;
  doc["minFreeHeap"] = deviceStatus.free;
  doc["temperature"] = deviceStatus.temperature;
  doc["counterReset"] = deviceStatus.counterReset;
  doc["ipAddress"] = WiFi.localIP().toString();

  uint64_t chipid = ESP.getEfuseMac();
  char macStr[18];
  sprintf(macStr, "%02X:%02X:%02X:%02X:%02X:%02X",
          (uint8_t)(chipid >> 40), (uint8_t)(chipid >> 32), (uint8_t)(chipid >> 24),
          (uint8_t)(chipid >> 16), (uint8_t)(chipid >> 8), (uint8_t)chipid);
  // Serial.println("Mac: " + String(macStr));
  doc["macAddress"] = String(macStr);

  String jsonString;
  serializeJson(doc, jsonString);

  // Publish to status topic
  if (mqttClient.publish(topicStatus, jsonString.c_str()))
  {
    Serial.printf("Status sent: %s\n", jsonString.c_str());
    // Blink OUT2 to indicate internet connectivity (only if connected)
    if (WiFi.status() == WL_CONNECTED && mqttClient.connected()) {
      blinkOutput2Connected();
    }
  }
  else
  {
    Serial.println("Failed to send device status");
    setSystemStatus("ERROR", "MQTT publish failed");
  }
}

void mqttCallback(char *topic, byte *payload, unsigned int length)
{
  DEBUG_PRINTF("=== MQTT CALLBACK TRIGGERED ===\n");
  DEBUG_PRINTF("Topic: %s\n", topic);
  
  // Handle Restart command
  if (strcmp(topic, topicRestart) == 0)
  {
    Serial.println("Restart command received - restarting ESP32...");
    ESP.restart();
  }
  
  // Handle OTA Update command
  if (strcmp(topic, topicOTA) == 0)
  {
    Serial.println("OTA command received - parsing payload...");
    
    // Parse JSON payload
    DynamicJsonDocument doc(1024);
    DeserializationError error = deserializeJson(doc, payload, length);
    
    if (error)
    {
      Serial.printf("OTA: JSON parse error: %s\n", error.c_str());
      setSystemStatus("ERROR", "OTA: Invalid JSON payload");
      return;
    }
    
    // Check if this update is for this specific device
    if (doc.containsKey("device"))
    {
      const char* targetDevice = doc["device"];
      if (strcmp(targetDevice, TopicMqtt) != 0)
      {
        Serial.printf("OTA: Update not for this device (target: %s, this: %s)\n", targetDevice, TopicMqtt);
        return;
      }
    }
    
    // Method 2: API endpoint + FTP URL (current)
    if (doc.containsKey("api") && doc.containsKey("ftpUrl"))
    {
      String apiEndpoint = doc["api"].as<String>();
      String ftpUrl = doc["ftpUrl"].as<String>();
      Serial.println("[MQTT] Received OTA command via API");
      performOTAUpdateViaAPI(apiEndpoint, ftpUrl);
      return;
    }

    // Method 1: Direct URL (for GitHub)
    if (doc.containsKey("url"))
    {
      const char* firmwareURL = doc["url"];
      Serial.println("[MQTT] Received OTA command via Direct URL");
      performOTAUpdate(firmwareURL);
      return;
    }
    
    // No valid OTA method found
    Serial.println("[MQTT ERROR] OTA payload is invalid. Expected 'url' or 'api'/'ftpUrl'.");
    setSystemStatus("ERROR", "OTA: Invalid payload");
  }

  // Match any 11223311A/Error/{anything} by prefix
  if (strncmp(topic, topicErrorPrefix, strlen(topicErrorPrefix)) == 0)
  {
    Serial.println("Error topic received - parsing payload...");
    parsePayload_IdLogLoss(payload, length, &receivedMessage, &companyInfo);
    Serial.printf("Parsed Idvoi: %s, Expected: %s\n", receivedMessage.Idvoi, TopicMqtt);

    if (strcmp(receivedMessage.Idvoi, TopicMqtt) == 0)
    {
      Serial.println("Idvoi matches - processing log loss...");
        if (uxQueueMessagesWaiting(logIdLossQueue) == 0)
        {
          TaskParams *taskParams = new TaskParams;
        taskParams->msg = new GetIdLogLoss(receivedMessage);
          taskParams->logIdLossQueue = logIdLossQueue;

        // Check heap before creating task
        size_t freeHeap = heap_caps_get_free_size(MALLOC_CAP_8BIT);
        Serial.printf("Heap before creating API task: %u bytes\n", freeHeap);
        
        if (freeHeap < 20000) {
          Serial.println("Not enough heap memory for API task");
          delete taskParams->msg;
          delete taskParams;
          return;
        }
        
        // Reduce stack size from 40960 to 16384
        if (xTaskCreate(callAPIServerGetLogLoss, "GetData", 16384, taskParams, 3, NULL) != pdPASS)
        {
          Serial.println("Failed to create log loss task");
            delete taskParams->msg;
            delete taskParams;
          }
        else
        {
          Serial.println("Log loss task created successfully");
        }
    }
    else
    {
        Serial.println("Log loss queue is not empty, skipping...");
      }
    }
    else
    {
      Serial.println("Idvoi does not match, ignoring message");
    }
  }
  
  // Handle ChangePrice command
  if (strcmp(topic, topicUpdatePrice) == 0)
  {
    DEBUG_PRINTF("[MQTT] UpdatePrice command received - parsing payload...\n");
    DEBUG_PRINTF("[MQTT] Current device MST: %s\n", companyInfo.Mst);
    DEBUG_PRINTF("[MQTT] Payload length: %d bytes\n", length);
    
    // Sync time from NTP before updating price to ensure accurate timestamp
    Serial.println("[MQTT] Syncing time from NTP server...");
    configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);
    
    // Wait a bit for time sync (non-blocking)
    struct tm timeinfo;
    int syncRetry = 0;
    while (!getLocalTime(&timeinfo) && syncRetry < 5)
    {
      vTaskDelay(pdMS_TO_TICKS(200));
      syncRetry++;
    }
    
    if (syncRetry < 5)
    {
      Serial.printf("[MQTT] ‚úì Time synced: %04d-%02d-%02d %02d:%02d:%02d\n",
                   timeinfo.tm_year + 1900, timeinfo.tm_mon + 1, timeinfo.tm_mday,
                   timeinfo.tm_hour, timeinfo.tm_min, timeinfo.tm_sec);
    }
    else
    {
      Serial.println("[MQTT] ‚ö†Ô∏è Time sync timeout, using system time");
    }
    
    // Print raw payload for debugging (debug only)
    DEBUG_PRINT("[MQTT] Raw payload: ");
    for (size_t i = 0; i < length && i < 500; i++) {
      DEBUG_PRINT((char)payload[i]);
    }
    DEBUG_PRINTLN("");
    
    // Parse JSON payload with new structure: {"topic":"...", "clientid":"...", "message":[...]}
    DynamicJsonDocument doc(2048); // Increase size for nested structure
    DeserializationError error = deserializeJson(doc, payload, length);
    
    if (error)
    {
      Serial.printf("[MQTT] UpdatePrice: JSON parse error: %s\n", error.c_str());
      setSystemStatus("ERROR", "UpdatePrice: Invalid JSON payload");
      return;
    }
    
    // Check if root is an object
    if (!doc.is<JsonObject>())
    {
      Serial.println("[MQTT] UpdatePrice: Payload is not a JSON object");
      setSystemStatus("ERROR", "UpdatePrice: Expected JSON object");
      return;
    }
    
    // Extract message array from the payload
    JsonArray priceArray = doc["message"];
    if (priceArray.isNull())
    {
      Serial.println("[MQTT] UpdatePrice: Missing 'message' field");
      setSystemStatus("ERROR", "UpdatePrice: Missing 'message' array");
      return;
    }
    
    Serial.printf("[MQTT] UpdatePrice: Received %d price entries\n", priceArray.size());
    
    // Parse and queue each price change request for RS485 task to process
    // Each device will only process messages that match its own IDChiNhanh and IdDevice
    int queued = 0;
    int skipped = 0;
    
    for (JsonObject entry : priceArray)
    {
      // Get item object
      JsonObject item = entry["item"];
      if (item.isNull())
      {
        Serial.println("[MQTT] Missing 'item' field, skipping...");
        skipped++;
        continue;
      }
      
      const char* idChiNhanh = item["IDChiNhanh"] | "";
      const char* idDevice = item["IdDevice"] | "";
      const char* nozzorle = item["Nozzorle"] | "";
      
      // Check if this is for current company (compare IDChiNhanh with MST)
      if (strlen(idChiNhanh) > 0 && strcmp(idChiNhanh, companyInfo.Mst) != 0)
      {
        DEBUG_PRINTF("[MQTT] Skipping - IDChiNhanh=%s doesn't match MST=%s\n", idChiNhanh, companyInfo.Mst);
        skipped++;
        continue;
      }
      
      DEBUG_PRINTF("[MQTT] ‚úÖ Processing Entry: IDChiNhanh=%s, IdDevice=%s, Nozzorle=%s\n", idChiNhanh, idDevice, nozzorle);
      
      // Handle null UnitPrice
      if (item["UnitPrice"].isNull())
      {
        Serial.println("[MQTT] UnitPrice is null, skipping...");
        skipped++;
        continue;
      }
      
      float unitPrice = item["UnitPrice"] | 0.0f;
      DEBUG_PRINTF("[MQTT] UnitPrice=%.2f\n", unitPrice);
      
      // Use Nozzorle field as RS485 Device ID directly
      // Nozzorle is provided as string (e.g., "11", "12", "13", ..., "20")
      if (strlen(nozzorle) == 0)
      {
        Serial.printf("[MQTT] Error: Missing Nozzorle field for IdDevice=%s, skipping...\n", idDevice);
        skipped++;
        continue;
      }
      
      // Parse Nozzorle to integer (already RS485 Device ID)
      uint8_t deviceIdNum = atoi(nozzorle);
      
      // Validate range (11-20)
      if (deviceIdNum < 11 || deviceIdNum > 20)
      {
        Serial.printf("[MQTT] Invalid Nozzorle: %s (must be 11-20), skipping...\n", nozzorle);
        skipped++;
        continue;
      }
      
      DEBUG_PRINTF("[MQTT] Nozzorle=%s -> RS485 DeviceID=%d\n", nozzorle, deviceIdNum);
      
      // Create price change request for this specific pump
      PriceChangeRequest request;
      request.deviceId = deviceIdNum;
      request.unitPrice = unitPrice;
      strncpy(request.idDevice, idDevice, sizeof(request.idDevice) - 1);
      request.idDevice[sizeof(request.idDevice) - 1] = '\0'; // Ensure null termination
      strncpy(request.idChiNhanh, idChiNhanh, sizeof(request.idChiNhanh) - 1);
      request.idChiNhanh[sizeof(request.idChiNhanh) - 1] = '\0'; // Ensure null termination
      
      // Queue the request for RS485 task to process
      if (xQueueSend(priceChangeQueue, &request, pdMS_TO_TICKS(100)) == pdTRUE)
      {
        queued++;
        Serial.printf("[MQTT] ‚úì Queued price change: IdDevice=%s -> PumpID=%d, Price=%.2f\n", idDevice, deviceIdNum, unitPrice);
      }
      else
      {
        skipped++;
        Serial.printf("[MQTT] ‚úó Failed to queue: Queue full for PumpID=%d\n", deviceIdNum);
      }
    }
    
    // Send summary
    Serial.printf("\n[MQTT] ChangePrice Summary: Queued=%d, Skipped=%d, Total=%d\n", 
                  queued, skipped, priceArray.size());
    Serial.printf("[MQTT] RS485 task will process %d price change(s)\n", queued);
    
    if (queued > 0) {
      setSystemStatus("OK", String("Queued ") + String(queued) + " price change(s)");
    } else {
      setSystemStatus("WARNING", "No price changes queued");
    }
  }
  
  // Handle GetPrice command - Request current prices from Flash
  if (strcmp(topic, topicGetPrice) == 0)
  {
    Serial.println("[MQTT] GetPrice command received - reading prices from Flash...");
    
    // Load latest prices from Flash
    NozzlePrices currentPrices;
    if (!loadNozzlePrices(currentPrices, flashMutex))
    {
      Serial.println("[MQTT] ‚úó Failed to load prices from Flash");
      setSystemStatus("ERROR", "Failed to load prices from Flash");
      return;
    }
    
    Serial.println("[MQTT] ‚úì Prices loaded from Flash, publishing...");
    
    // Build response topic: {IdChiNhanh}/ResponsePrice
    char responseTopic[64];
    snprintf(responseTopic, sizeof(responseTopic), "%s/ResponsePrice", companyInfo.Mst);
    
    // Create JSON response with all nozzle prices
    DynamicJsonDocument doc(2048); // Increased size for 10 nozzles with all fields
    doc["topic"] = companyInfo.CompanyId;
    doc["clientid"] = TopicMqtt;
    doc["timestamp"] = currentPrices.lastUpdate;
    
    // Create prices array for all 10 nozzles (11-20)
    JsonArray pricesArray = doc.createNestedArray("prices");
    for (int i = 0; i < 10; i++)
    {
      JsonObject nozzle = pricesArray.createNestedObject();
      nozzle["Nozzle"] = currentPrices.nozzles[i].nozzorle[0] ? currentPrices.nozzles[i].nozzorle : String(11 + i);
      nozzle["IdDevice"] = currentPrices.nozzles[i].idDevice;
      nozzle["UnitPrice"] = currentPrices.nozzles[i].price;
      
      // Format timestamp to dd/mm/yyyy-HH:MM:SS
      time_t timestamp = currentPrices.nozzles[i].updatedAt;
      if (timestamp > 0) {
        struct tm *timeinfo = localtime(&timestamp);
        char formattedTime[32];
        snprintf(formattedTime, sizeof(formattedTime), "%02d/%02d/%04d-%02d:%02d:%02d",
                 timeinfo->tm_mday, timeinfo->tm_mon + 1, timeinfo->tm_year + 1900,
                 timeinfo->tm_hour, timeinfo->tm_min, timeinfo->tm_sec);
        nozzle["UpdatedAt"] = formattedTime;
      } else {
        nozzle["UpdatedAt"] = "N/A";  // Ch∆∞a c√≥ gi√° tr·ªã
      }
    }
    
    // Serialize and publish
    String jsonString;
    serializeJson(doc, jsonString);
    
    if (mqttClient.publish(responseTopic, jsonString.c_str()))
    {
      Serial.printf("[MQTT] ‚úì Published ResponsePrice to %s\n", responseTopic);
      Serial.printf("[MQTT] Payload: %s\n", jsonString.c_str());
      setSystemStatus("OK", "Price data published");
    }
    else
    {
      Serial.printf("[MQTT] ‚úó Failed to publish ResponsePrice to %s\n", responseTopic);
      setSystemStatus("ERROR", "Failed to publish price data");
    }
  }
  
  Serial.println("=== MQTT CALLBACK FINISHED ===\n");
}

void sendMQTTData(const PumpLog &log)
{
  String jsonData = convertPumpLogToJson(log);

  int retryCount = 0;
  const int maxRetries = 3;
  bool mqttSuccess = false;
  esp_task_wdt_reset();

  while (retryCount < maxRetries)
  {
    if (mqttClient.publish(fullTopic, jsonData.c_str()))
    {
      Serial.println("MQTT data sent successfully");
      mqttSuccess = true;
      break;
    }
    else
    {
      Serial.printf("MQTT send failed (Attempt %d/%d)\n", retryCount + 1, maxRetries);
      retryCount++;
      vTaskDelay(pdMS_TO_TICKS(500));
    }
    esp_task_wdt_reset();
    yield();
  }

  // Prepare updated log with MQTT status
  PumpLog updatedLog = log;
  updatedLog.mqttSentTime = time(NULL); // Timestamp from Google NTP (success or failure)
  
  if (mqttSuccess)
  {
    updatedLog.mqttSent = 1; // Success
    Serial.printf("‚úì Log %d: MQTT sent successfully at %ld\n", 
                  updatedLog.viTriLogData, updatedLog.mqttSentTime);
  }
  else
  {
    updatedLog.mqttSent = 0; // Failed
    Serial.println("ERROR: MQTT send failed after maximum retries");
    Serial.printf("‚úó Log %d: MQTT failed at %ld\n", 
                  updatedLog.viTriLogData, updatedLog.mqttSentTime);
    setSystemStatus("ERROR", "MQTT send failed after " + String(maxRetries) + " retries");
  }

  // Save to Flash at position viTriLogData (1-5000)
  if (updatedLog.viTriLogData >= 1 && updatedLog.viTriLogData <= MAX_LOGS)
  {
    if (xSemaphoreTake(flashMutex, pdMS_TO_TICKS(100)) == pdTRUE)
    {
      File dataFile = LittleFS.open(FLASH_DATA_FILE, "r+");
      if (!dataFile) {
        dataFile = LittleFS.open(FLASH_DATA_FILE, "w");
      }
      
      if (dataFile)
      {
        uint32_t offset = (updatedLog.viTriLogData - 1) * sizeof(PumpLog);
        dataFile.seek(offset, SeekSet);
        dataFile.write((const uint8_t*)&updatedLog, sizeof(PumpLog));
        dataFile.close();
        Serial.printf("üíæ Log %d saved to Flash (mqttSent=%d)\n", updatedLog.viTriLogData, updatedLog.mqttSent);
      }
      else
      {
        Serial.println("ERROR: Failed to open Flash file for log save");
      }
      xSemaphoreGive(flashMutex);
    }
    else
    {
      Serial.printf("‚ö†Ô∏è Flash mutex timeout for Log %d\n", updatedLog.viTriLogData);
    }
  }
  else
  {
    Serial.printf("ERROR: Invalid viTriLogData=%d\n", updatedLog.viTriLogData);
  }
}

void readRS485Data(byte *buffer)
{
  if (!Serial2.available())
  {
    return;
  }
  
  // Limit processing to prevent overflow from TTL boot spam
  static unsigned long lastReadTime = 0;
  static int consecutiveReads = 0;
  unsigned long now = millis();
  
  if (now - lastReadTime < 50)
  {
    consecutiveReads++;
    if (consecutiveReads > 20)
    {
      Serial.println("[RS485 READ] ‚ö†Ô∏è Too many consecutive reads, clearing buffer to prevent overflow");
      while (Serial2.available())
      {
        Serial2.read();
      }
      consecutiveReads = 0;
      return;
    }
  }
  else
  {
    consecutiveReads = 0;
  }
  lastReadTime = now;

  // Peek first byte to determine message type
  int firstByte = Serial2.peek();
  
  // Case 0: Echo of sent command - Format: [9][ID][...] (10 bytes) - DISCARD IT
  if (firstByte == 9 && Serial2.available() >= 10)
  {
    uint8_t echoBuffer[10];
    Serial2.readBytes(echoBuffer, 10);
    Serial.printf("[RS485 READ] Discarding echo: [0x%02X][0x%02X]...\n", echoBuffer[0], echoBuffer[1]);
    return;
  }
  
  // Case 1: Price Change Response - Format: [7][ID][S/E][8] (4 bytes)
  if (firstByte == 7 && Serial2.available() >= 4)
  {
    uint8_t priceResponse[4];
    if (Serial2.readBytes(priceResponse, 4) == 4)
    {
      // Validate format: [7][ID][Status][8]
      if (priceResponse[0] == 7 && priceResponse[3] == 8)
      {
        uint8_t deviceId = priceResponse[1];
        char status = priceResponse[2];
        
        // Validate deviceId is in valid range (11-20)
        if (deviceId < 11 || deviceId > 20)
        {
          // Reduce log spam - only log every 10th invalid response
          static int invalidCount = 0;
          invalidCount++;
          if (invalidCount % 10 == 0)
          {
            Serial.printf("[RS485 READ] ‚ö†Ô∏è Ignoring invalid DeviceID=%d (count: %d, valid range: 11-20)\n", deviceId, invalidCount);
          }
          return;
        }
        
        // Log valid response (debug only)
        DEBUG_PRINTF("\n[RS485 READ] Price Change Response: ");
        for (int i = 0; i < 4; i++) {
          if (i == 2) {
            DEBUG_PRINTF("'%c'(0x%02X) ", priceResponse[i], priceResponse[i]);
          } else {
            DEBUG_PRINTF("0x%02X ", priceResponse[i]);
          }
        }
        DEBUG_PRINTLN("");
        
        // Parse status
        if (status == 'S')
        {
          DEBUG_PRINTF("[RS485 READ] ‚úì SUCCESS - DeviceID=%d price updated successfully\n", deviceId);
          
          // Save price to Flash for this nozzle
          if (deviceId == lastPriceChangeRequest.deviceId)
          {
            // Convert deviceId to nozzorle string
            char nozzorleStr[4];
            snprintf(nozzorleStr, sizeof(nozzorleStr), "%d", deviceId);
            updateNozzlePrice(nozzorleStr, lastPriceChangeRequest.idDevice, 
                            lastPriceChangeRequest.unitPrice, nozzlePrices, flashMutex);
          }
          
          // Publish Complete response to MQTT
          if (mqttClient.connected() && deviceId == lastPriceChangeRequest.deviceId)
          {
            // Build response topic: {CompanyId}/FinishPrice
            char responseTopic[100];
            snprintf(responseTopic, sizeof(responseTopic), "%s/FinishPrice", companyInfo.CompanyId);
            
            // Build JSON response with new structure
            DynamicJsonDocument doc(1024);
            doc["topic"] = lastPriceChangeRequest.idChiNhanh;
            
            // Build clientid: {IDChiNhanh}/GetStatus/{TopicMqtt}
            char clientId[100];
            snprintf(clientId, sizeof(clientId), "%s/GetStatus/%s", 
                     lastPriceChangeRequest.idChiNhanh, TopicMqtt);
            doc["clientid"] = clientId;
            
            // Get timestamp from the nozzle that was just updated
            int nozzleIndex = deviceId - 11;  // Map 11-20 to index 0-9
            time_t updatedTimestamp = nozzlePrices.nozzles[nozzleIndex].updatedAt;
            
            // Format timestamp to dd/mm/yyyy-HH:MM:SS
            struct tm *timeinfo = localtime(&updatedTimestamp);
            char formattedTime[32];
            snprintf(formattedTime, sizeof(formattedTime), "%02d/%02d/%04d-%02d:%02d:%02d",
                     timeinfo->tm_mday, timeinfo->tm_mon + 1, timeinfo->tm_year + 1900,
                     timeinfo->tm_hour, timeinfo->tm_min, timeinfo->tm_sec);
            
            doc["timestamp"] = formattedTime;
            
            // Build message array
            JsonArray messageArray = doc.createNestedArray("message");
            JsonObject entry = messageArray.createNestedObject();
            entry["Key"] = "UpdatePrice";
            
            JsonObject item = entry.createNestedObject("item");
            item["IDChiNhanh"] = lastPriceChangeRequest.idChiNhanh;
            item["IdDevice"] = lastPriceChangeRequest.idDevice;
            item["UnitPrice"] = lastPriceChangeRequest.unitPrice;
            item["UpdatedAt"] = formattedTime;  // Formatted timestamp t·ª´ Flash
            
            String jsonString;
            serializeJson(doc, jsonString);
            
            if (mqttClient.publish(responseTopic, jsonString.c_str()))
            {
              Serial.printf("[RS485 READ] ‚úì Published FinishPrice to %s: %s\n", responseTopic, jsonString.c_str());
            }
            else
            {
              Serial.printf("[RS485 READ] ‚úó Failed to publish FinishPrice to %s\n", responseTopic);
            }
          }
        }
        else if (status == 'E')
        {
          Serial.printf("[RS485 READ] ‚úó ERROR - DeviceID=%d rejected price update (KPL returned 'E')\n", deviceId);
          Serial.printf("[RS485 READ] ‚ö†Ô∏è Error response ignored - NOT publishing to MQTT\n");
          // Do NOT publish Error response to MQTT - only Success is published
        }
        else
        {
          Serial.printf("[RS485 READ] ‚úó UNKNOWN STATUS - DeviceID=%d, Status='%c' (0x%02X)\n", 
                        deviceId, status, status);
          Serial.printf("[RS485 READ] ‚ö†Ô∏è Unknown status ignored - NOT publishing to MQTT\n");
        }
      }
      else
      {
        Serial.printf("[RS485 READ] Invalid price response format: [0x%02X][0x%02X][0x%02X][0x%02X]\n",
                      priceResponse[0], priceResponse[1], priceResponse[2], priceResponse[3]);
      }
    }
    return;
  }
  
  // Case 2: Pump Log Data - Format: [1][2]...[3][checksum][4] (32 bytes)
  if (firstByte == 1 && Serial2.available() >= LOG_SIZE)
  {
    // Set timeout for reading to prevent blocking
    unsigned long readStartTime = millis();
    size_t bytesRead = Serial2.readBytes(buffer, LOG_SIZE);
    unsigned long readDuration = millis() - readStartTime;
    
    if (readDuration > 1000)
    {
      Serial.printf("[RS485 READ] ‚ö†Ô∏è Read timeout: %lu ms for %d bytes\n", readDuration, bytesRead);
      return;
    }
    
    if (bytesRead == LOG_SIZE)
    {
      // Validate data
      uint8_t calculatedChecksum = calculateChecksum_LogData(buffer, LOG_SIZE);
      uint8_t receivedChecksum = buffer[30];

      if (calculatedChecksum == receivedChecksum &&
          buffer[0] == 1 && buffer[1] == 2 &&
          buffer[29] == 3 && buffer[31] == 4)
      {
        // Process valid pump log data
        PumpLog log;
        ganLog(buffer, log);
        
        // ki·ªÉm tra mqttQueue c√≥ d·ªØ li·ªáu kh√¥ng
        if (uxQueueMessagesWaiting(mqttQueue) > 0)
        {
          Serial.println("MQTT queue is not empty, skipping...");
          return;
        }

        if (xQueueSend(mqttQueue, &log, pdMS_TO_TICKS(100)) == pdTRUE)
        {
          Serial.println("Log data queued for MQTT");
          // Trigger relay
          xTaskCreate(ConnectedKPLBox, "ConnectedKPLBox", 1024, NULL, 3, NULL);
        }
      }
      else
      {
        Serial.println("[RS485 READ] Invalid pump log: checksum or format error");
      }
    }
    else
    {
      Serial.printf("[RS485 READ] ‚ö†Ô∏è Incomplete read: expected %d bytes, got %d\n", LOG_SIZE, bytesRead);
    }
    return;
  }
  
  // Case 3: Unknown or incomplete data - wait for more data or clear buffer
  if (Serial2.available() > 0 && Serial2.available() < 4)
  {
    // Not enough data yet, wait for next iteration
    return;
  }
  
  // Case 4: Buffer overflow protection - if buffer is suspiciously full, clear it
  if (Serial2.available() > 100)
  {
    Serial.printf("[RS485 READ] ‚ö†Ô∏è Buffer overflow detected (%d bytes), clearing...\n", Serial2.available());
    while (Serial2.available())
    {
      Serial2.read();
    }
    return;
  }
  
  // Case 5: Invalid first byte - clear one byte and try again
  if (firstByte != 1 && firstByte != 7 && firstByte != 9)
  {
    // Reduce spam by only logging every 10th invalid byte
    static int invalidByteCount = 0;
    invalidByteCount++;
    
    if (invalidByteCount % 10 == 0)
    {
      Serial.printf("[RS485 READ] Unknown first byte: 0x%02X (count: %d), discarding...\n", firstByte, invalidByteCount);
    }
    
    Serial2.read(); // Discard invalid byte
    
    // Reset counter every 100 invalid bytes to prevent overflow
    if (invalidByteCount >= 100)
    {
      invalidByteCount = 0;
    }
  }
}

// vi·∫øt ch∆∞∆°ng tr√¨nh resend l·∫°i l·ªánh rs485 v·ªõi id trong LogIdLossQueue ƒë·ªÉ ƒë·ªçc gi√° tr·ªã l√™n
// D·ª±a v√†o getLogData ƒë·ªÉ resend l·∫°i l·ªánh rs485 v·ªõi id trong LogIdLossQueue ƒë·ªÉ ƒë·ªçc gi√° tr·ªã l√™n
// D·ª±a v√†o sendLogRequest ƒë·ªÉ resend l·∫°i l·ªánh rs485 v·ªõi id trong LogIdLossQueue ƒë·ªÉ ƒë·ªçc gi√° tr·ªã l√™n


void resendLogRequest(void *param)
{
  Serial.println("ResendLogRequest task started");
  esp_task_wdt_add(NULL);
  while (true)
  {
    DtaLogLoss dataLog;
    // ki·ªÉm tra xem c√≥ d·ªØ li·ªáu trong logIdLossQueue kh√¥ng
    if (uxQueueMessagesWaiting(logIdLossQueue) > 0) 
    {
      if (xQueueReceive(logIdLossQueue, &dataLog, 0) == pdTRUE)
      {
        Serial.println("Resending log request for ID: " + String(dataLog.Logid));
        sendLogRequest(static_cast<uint32_t>(dataLog.Logid));
      }
    }
    vTaskDelay(pdMS_TO_TICKS(1000));
    esp_task_wdt_reset();
    yield();
  }
}

void sendPriceChangeCommand(const PriceChangeRequest &request)
{
  // Save request for response publishing
  lastPriceChangeRequest = request;
  
  Serial.printf("\n[PRICE CHANGE] Sending command: DeviceID=%d, IdDevice=%s, IDChiNhanh=%s, Price=%.2f\n", 
                request.deviceId, request.idDevice, request.idChiNhanh, request.unitPrice);
  
  // Convert price to 6-digit ASCII string (Char 0-5)
  // Format as integer with leading zeros (6 digits for Char 0-5)
  char priceStr[7];
  snprintf(priceStr, sizeof(priceStr), "%06d", (int)request.unitPrice);
  
  // Build buffer according to protocol (10 bytes total)
  // Protocol: [PM(9)] [ID_Device] [Price_Char0-5 (6 bytes)] [Checksum] [LF(10)]
  uint8_t buffer[10];
  buffer[0] = 9;                 // PM command (Dec 9)
  buffer[1] = request.deviceId;  // ID Device (11-20)
  buffer[2] = priceStr[5];       // ƒê∆°n gi√° Char(0) - ASCII
  buffer[3] = priceStr[4];       // ƒê∆°n gi√° Char(1) - ASCII
  buffer[4] = priceStr[3];       // ƒê∆°n gi√° Char(2) - ASCII
  buffer[5] = priceStr[2];       // ƒê∆°n gi√° Char(3) - ASCII
  buffer[6] = priceStr[1];       // ƒê∆°n gi√° Char(4) - ASCII
  buffer[7] = priceStr[0];       // ƒê∆°n gi√° Char(5) - ASCII
  
  // Calculate checksum: 0x5A XOR all data bytes (ID + Price chars)
  uint8_t checksum = 0x5A ^ buffer[1] ^ buffer[2] ^ buffer[3] ^ buffer[4] ^ buffer[5] ^ buffer[6] ^ buffer[7];
  buffer[8] = checksum;          // Char(Cksum)
  buffer[9] = 10;                // End byte (Dec 10 = LF)
  
  // Send via RS485 (Serial2)
  Serial2.write(buffer, sizeof(buffer));
  Serial2.flush(); // Wait for transmission to complete
  
  Serial.printf("[PRICE CHANGE] ‚úì Command sent to KPL: DeviceID=%d, Price=%.2f -> ASCII: %s\n", 
                request.deviceId, request.unitPrice, priceStr);
  Serial.print("[PRICE CHANGE] Data (HEX): ");
  for (int i = 0; i < sizeof(buffer); i++) {
    Serial.printf("0x%02X ", buffer[i]);
  }
  Serial.print("\n[PRICE CHANGE] Data (ASCII): ");
  for (int i = 0; i < sizeof(buffer); i++) {
    if (i >= 2 && i <= 7) {
      Serial.printf("'%c' ", buffer[i]);
    } else {
      Serial.printf("0x%02X ", buffer[i]);
    }
  }
  Serial.println();
  Serial.println("[PRICE CHANGE] Response will be handled by readRS485Data()");
}

void ConnectedKPLBox(void *param)
{
  digitalWrite(OUT1, HIGH);
  vTaskDelay(pdMS_TO_TICKS(200));
  digitalWrite(OUT2, HIGH);
  vTaskDelay(pdMS_TO_TICKS(200));
  digitalWrite(OUT1, LOW);
  digitalWrite(OUT2, LOW);
  vTaskDelete(NULL);
}

void BlinkOutput2Task(void *param)
{
  // Two quick blinks on OUT2
  for (int i = 0; i < 1; i++) {
    digitalWrite(OUT1, HIGH);
    digitalWrite(OUT2, HIGH);
    vTaskDelay(pdMS_TO_TICKS(120));
    digitalWrite(OUT1, LOW);
    digitalWrite(OUT2, LOW);
    vTaskDelay(pdMS_TO_TICKS(120));
  }
  vTaskDelete(NULL);
}

void blinkOutput2Connected()
{
  // Fire-and-forget non-blocking blink
  xTaskCreate(BlinkOutput2Task, "BlinkOUT2", 1024, NULL, 1, NULL);
}

void wifiRescanTask(void *param)
{
  // Perform a one-time WiFi scan and update list for config portal
  if (wifiManager != nullptr)
  {
    Serial.println("[Rescan] Starting WiFi rescan...");
    wifiManager->scanWiFi();
    Serial.println("[Rescan] WiFi rescan completed.");
  }
  vTaskDelete(NULL);
}

// ============================================================================
// OTA UPDATE FUNCTION
// ============================================================================

// ============================================================================
// OTA UPDATE FROM DIRECT URL (GitHub)
// ============================================================================
void performOTAUpdate(const char* firmwareURL)
{
  Serial.printf("\n\n=== STARTING OTA UPDATE FROM URL ===\n");
  Serial.printf("[OTA INFO] Firmware URL: %s\n", firmwareURL);
  digitalWrite(OUT2, HIGH);

  if (WiFi.status() != WL_CONNECTED)
  {
    Serial.println("[OTA ERROR] WiFi not connected.");
    digitalWrite(OUT2, LOW);
    return;
  }
  
  HTTPClient http;
  WiFiClientSecure client; // GitHub requires HTTPS

  client.setInsecure(); // For simplicity
  http.begin(client, firmwareURL);
  
  // GitHub requires these headers
  http.addHeader("User-Agent", "ESP32-OTA-Client");
  http.addHeader("Accept", "application/octet-stream");
  http.setFollowRedirects(HTTPC_STRICT_FOLLOW_REDIRECTS); // GitHub uses redirects
  http.setTimeout(30000); // 30 seconds timeout

  Serial.println("[OTA INFO] Connecting to server...");
  int httpCode = http.GET();

  if (httpCode != HTTP_CODE_OK)
  {
    Serial.printf("[OTA ERROR] HTTP GET failed. Code: %d, Error: %s\n", httpCode, http.errorToString(httpCode).c_str());
    http.end();
    digitalWrite(OUT2, LOW);
    return;
  }

  int contentLength = http.getSize();
  if (contentLength <= 0) {
    Serial.println("[OTA ERROR] Invalid Content-Length.");
    http.end();
    digitalWrite(OUT2, LOW);
    return;
  }

  Serial.printf("[OTA INFO] Firmware size: %d bytes\n", contentLength);

  if (!Update.begin(contentLength))
  {
    Serial.printf("[OTA ERROR] Not enough space for OTA. Error: %s\n", Update.errorString());
    http.end();
    digitalWrite(OUT2, LOW);
    return;
  }

  Serial.println("[OTA INFO] Starting firmware download and flash...");
  
  // Publish OTA started status
  if (mqttClient.connected())
  {
    DynamicJsonDocument doc(256);
    doc["status"] = "OTA_DOWNLOADING";
    doc["progress"] = 0;
    doc["device"] = TopicMqtt;
    String jsonString;
    serializeJson(doc, jsonString);
    mqttClient.publish(topicStatus, jsonString.c_str());
    mqttClient.loop();
  }
  
  // Download with progress tracking
  WiFiClient* stream = http.getStreamPtr();
  uint8_t buff[512];
  size_t written = 0;
  int lastPercent = -1;
  
  while (http.connected() && written < contentLength)
  {
    size_t available = stream->available();
    if (available)
    {
      size_t len = stream->readBytes(buff, sizeof(buff));
      if (len > 0)
      {
        if (Update.write(buff, len) != len)
        {
          Serial.println("[OTA ERROR] Write failed during download");
          Update.abort();
          http.end();
          digitalWrite(OUT2, LOW);
          
          // Publish failure
          if (mqttClient.connected())
          {
            DynamicJsonDocument doc(256);
            doc["status"] = "OTA_FAILED";
            doc["error"] = "Write failed";
            doc["device"] = TopicMqtt;
            String jsonString;
            serializeJson(doc, jsonString);
            mqttClient.publish(topicStatus, jsonString.c_str());
            mqttClient.loop();
          }
          return;
        }
        written += len;
        
        // Report progress every 5%
        int percent = (written * 100) / contentLength;
        if (percent != lastPercent && percent % 5 == 0)
        {
          Serial.printf("[OTA PROGRESS] %d%% (%u / %d bytes)\n", percent, written, contentLength);
          lastPercent = percent;
          
          // Publish progress to MQTT
          if (mqttClient.connected())
          {
            DynamicJsonDocument doc(256);
            doc["status"] = "OTA_DOWNLOADING";
            doc["progress"] = percent;
            doc["device"] = TopicMqtt;
            String jsonString;
            serializeJson(doc, jsonString);
            mqttClient.publish(topicStatus, jsonString.c_str());
            mqttClient.loop();
          }
        }
      }
    }
    delay(1);
  }

  if (written != contentLength)
  {
    Serial.printf("[OTA ERROR] Download incomplete. Written: %d, Expected: %d\n", written, contentLength);
    Update.abort();
    http.end();
    digitalWrite(OUT2, LOW);
    
    // Publish failure
    if (mqttClient.connected())
    {
      DynamicJsonDocument doc(256);
      doc["status"] = "OTA_FAILED";
      doc["error"] = "Incomplete download";
      doc["device"] = TopicMqtt;
      String jsonString;
      serializeJson(doc, jsonString);
      mqttClient.publish(topicStatus, jsonString.c_str());
      mqttClient.loop();
    }
    return;
  }

  if (!Update.end(true))
  {
    Serial.printf("[OTA ERROR] Finalizing update failed. Error: %s\n", Update.errorString());
    digitalWrite(OUT2, LOW);
    
    // Publish failure
    if (mqttClient.connected())
    {
      DynamicJsonDocument doc(256);
      doc["status"] = "OTA_FAILED";
      doc["error"] = Update.errorString();
      doc["device"] = TopicMqtt;
      String jsonString;
      serializeJson(doc, jsonString);
      mqttClient.publish(topicStatus, jsonString.c_str());
      mqttClient.loop();
    }
    return;
  }

  Serial.println("[OTA SUCCESS] Update successful! Restarting...");
  
  // Publish 100% complete before restart
  if (mqttClient.connected())
  {
    DynamicJsonDocument doc(256);
    doc["status"] = "OTA_SUCCESS";
    doc["progress"] = 100;
    doc["device"] = TopicMqtt;
    String jsonString;
    serializeJson(doc, jsonString);
    mqttClient.publish(topicStatus, jsonString.c_str());
    mqttClient.loop();
  }
  
  delay(1000);
  ESP.restart();
}


// ============================================================================
// OTA UPDATE VIA API FUNCTION (REWRITTEN)
// ============================================================================

void performOTAUpdateViaAPI(const String& apiEndpoint, const String& ftpUrl)
{
  Serial.println("\n\n=== STARTING OTA UPDATE VIA API (v2) ===");
  digitalWrite(OUT2, HIGH); // Turn on OTA indicator LED

  // 1. PRE-FLIGHT CHECKS
  // ===============================================
  if (WiFi.status() != WL_CONNECTED)
  {
    Serial.println("[OTA ERROR] WiFi not connected.");
    setSystemStatus("ERROR", "OTA: No WiFi");
    digitalWrite(OUT2, LOW);
    return;
  }

  size_t freeHeap = heap_caps_get_free_size(MALLOC_CAP_8BIT);
  Serial.printf("[OTA INFO] Free heap before OTA: %u bytes\n", freeHeap);
  if (freeHeap < 60000) // Need enough heap for SSL client and buffers
  {
    Serial.println("[OTA ERROR] Not enough heap memory for OTA update.");
    setSystemStatus("ERROR", "OTA: Low memory");
    digitalWrite(OUT2, LOW);
    return;
  }

  // 2. HTTP CLIENT SETUP
  // ===============================================
  HTTPClient http;
  WiFiClient *streamClient = nullptr;

  if (apiEndpoint.startsWith("https://"))
  {
    WiFiClientSecure *secureClient = new WiFiClientSecure;
    if (secureClient) {
      secureClient->setInsecure(); // For simplicity. In production, consider adding root CA.
      secureClient->setTimeout(20); // 20-second timeout for socket operations
      streamClient = secureClient;
      http.begin(*secureClient, apiEndpoint);
      Serial.println("[OTA INFO] Using HTTPS connection.");
    } else {
      Serial.println("[OTA ERROR] Failed to create secure client.");
      digitalWrite(OUT2, LOW);
      return;
    }
  }
  else
  {
    WiFiClient *client = new WiFiClient;
    streamClient = client;
    http.begin(*client, apiEndpoint);
    Serial.println("[OTA INFO] Using HTTP connection.");
  }
  
  // Set headers to mimic a browser, which is the most reliable configuration we've found
  http.addHeader("Content-Type", "application/json");
  http.addHeader("User-Agent", "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/91.0.4472.124 Safari/537.36");
  http.addHeader("Accept", "application/json, text/plain, */*");
  http.addHeader("Connection", "close");
  http.setTimeout(60000); // 60-second timeout for the entire request

  // Prepare JSON body
  DynamicJsonDocument postDoc(1024);
  postDoc["FtpUrl"] = ftpUrl.c_str();
  String postBody;
  serializeJson(postDoc, postBody);

  // 3. SEND REQUEST AND CHECK RESPONSE
  // ===============================================
  Serial.printf("[OTA INFO] Sending POST request to %s\n", apiEndpoint.c_str());
  Serial.printf("[OTA INFO] Body: %s\n", postBody.c_str());
  int httpCode = http.POST(postBody);

  if (httpCode != HTTP_CODE_OK)
  {
    Serial.printf("[OTA ERROR] API request failed. HTTP Code: %d\n", httpCode);
    Serial.printf("[OTA ERROR] Server response: %s\n", http.getString().c_str());
    setSystemStatus("ERROR", "OTA: API Error " + String(httpCode));
    http.end();
    digitalWrite(OUT2, LOW);
    return;
  }

  Serial.println("[OTA INFO] API request successful. Receiving firmware...");

  // 4. BEGIN FIRMWARE UPDATE PROCESS
  // ===============================================
  int contentLength = http.getSize();
  Serial.printf("[OTA INFO] Content-Length: %d bytes\n", (contentLength == -1 ? 0 : contentLength));

  bool updateStarted = false;
  if (contentLength > 0) {
    updateStarted = Update.begin(contentLength);
  } else {
    // If size is unknown, start with max size.
    updateStarted = Update.begin(UPDATE_SIZE_UNKNOWN);
  }

  if (!updateStarted)
  {
    Serial.printf("[OTA ERROR] Not enough space for OTA. Error: %s\n", Update.errorString());
    setSystemStatus("ERROR", "OTA: " + String(Update.errorString()));
    http.end();
    digitalWrite(OUT2, LOW);
    return;
  }

  // 5. DOWNLOAD AND FLASH LOOP
  // ===============================================
  Serial.println("[OTA INFO] Starting firmware download and flash...");
  
  // Disable Watchdog Timer for the update process
  disableCore0WDT();
  #if CONFIG_FREERTOS_UNICORE == 0
  disableCore1WDT();
  #endif

  size_t written = 0;
  uint8_t buff[512] = {0};
  unsigned long startTime = millis();
  int lastPercent = -1; // Track progress percentage
  
  WiFiClient* stream = http.getStreamPtr();
  while (http.connected() && (contentLength == -1 || written < contentLength))
  {
    size_t available = stream->available();
    if (available)
    {
      size_t len = stream->readBytes(buff, sizeof(buff));
      if (len > 0)
      {
        if (Update.write(buff, len) != len)
        {
          Serial.printf("[OTA ERROR] Write failed at %u bytes. Error: %s\n", written, Update.errorString());
          
          // CRITICAL DIAGNOSTIC: Dump the failing buffer
          Serial.println("--- BEGIN FAILED BUFFER DUMP (HEX) ---");
          for(int i=0; i<len; i++) {
            Serial.printf("%02X ", buff[i]);
            if ((i+1) % 16 == 0) Serial.println();
          }
          Serial.println("\n--- END FAILED BUFFER DUMP ---");
          
          Update.abort();
          goto ota_end; // Jump to cleanup code
        }
        written += len;

        // Progress reporting
        if (contentLength > 0) {
            int percent = (written * 100) / contentLength;
            if (percent != lastPercent && percent % 5 == 0)
            {
              Serial.printf("[OTA PROGRESS] %d%% (%u / %d bytes)\n", percent, written, contentLength);
              lastPercent = percent;
              
              // Publish progress to MQTT
              if (mqttClient.connected())
              {
                DynamicJsonDocument doc(256);
                doc["status"] = "OTA_DOWNLOADING";
                doc["progress"] = percent;
                doc["device"] = TopicMqtt;
                String jsonString;
                serializeJson(doc, jsonString);
                mqttClient.publish(topicStatus, jsonString.c_str());
                mqttClient.loop();
              }
            }
        } else {
            // Unknown size, report every 50KB
            if (written % 51200 == 0) {
              Serial.printf("[OTA PROGRESS] %u bytes written\n", written);
              
              // Publish progress to MQTT
              if (mqttClient.connected())
              {
                DynamicJsonDocument doc(256);
                doc["status"] = "OTA_DOWNLOADING";
                doc["bytes"] = written;
                doc["device"] = TopicMqtt;
                String jsonString;
                serializeJson(doc, jsonString);
                mqttClient.publish(topicStatus, jsonString.c_str());
                mqttClient.loop();
              }
            }
        }
      }
    }
    // Yield to other tasks and feed WDT
    vTaskDelay(1); 
  }

ota_end:
  // 6. FINALIZE AND CLEANUP
  // ===============================================
  unsigned long duration = (millis() - startTime) / 1000;
  Serial.printf("\n[OTA INFO] Download finished in %lu seconds. Total bytes: %u\n", duration, written);

  // Re-enable Watchdog Timer
  enableCore0WDT();
  #if CONFIG_FREERTOS_UNICORE == 0
  enableCore1WDT();
  #endif
  
  http.end();
  digitalWrite(OUT2, LOW);

  if (Update.hasError())
  {
    Serial.printf("[OTA ERROR] Update failed with error: %s\n", Update.errorString());
    setSystemStatus("ERROR", "OTA: " + String(Update.errorString()));
  }
  else if (Update.isFinished())
  {
    if (Update.end(true)) // true to set the new partition as bootable
    {
      Serial.println("[OTA SUCCESS] Update successful! Restarting...");
      delay(1000);
      ESP.restart();
    }
    else
    {
      Serial.printf("[OTA ERROR] Finalizing update failed. Error: %s\n", Update.errorString());
      setSystemStatus("ERROR", "OTA: Finalize failed");
    }
  }
  else
  {
      Serial.println("[OTA ERROR] Update did not complete.");
      setSystemStatus("ERROR", "OTA: Incomplete");
  }
  
  Serial.println("=== OTA UPDATE VIA API PROCESS COMPLETED ===");
}